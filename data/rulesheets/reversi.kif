; Reversi
; Parts loosely adapted from othello-comp2007
; GDL by Alex Landau

(role black)
(role red)
(opponent black red)
(opponent red black)
(init (cell 4 4 black))
(init (cell 4 5 red))
(init (cell 5 4 red))
(init (cell 5 5 black))
(init (control black))

(oppDir nw se)
(oppDir ne sw)
(oppDir se nw)
(oppDir sw ne)
(oppDir n s)
(oppDir e w)
(oppDir s n)
(oppDir w e)

(dir n 0 p1)
(dir s 0 n1)
(dir e p1 0)
(dir w n1 0)
(dir nw n1 p1)
(dir ne p1 p1)
(dir se p1 n1)
(dir sw n1 n1)

(<= (hasOtherColorInDir ?x1 ?y1 ?dir)
    (true (cell ?x1 ?y1 ?color))
    (opponent ?color ?otherColor)
    (nextCellInDir ?x1 ?y1 ?dir ?x2 ?y2)
    (true (cell ?x2 ?y2 ?otherColor)))
(<= (hasOtherColorInDir ?x1 ?y1 ?dir)
    (true (cell ?x1 ?y1 ?color))
    (nextCellInDir ?x1 ?y1 ?dir ?x2 ?y2)
    (true (cell ?x2 ?y2 ?color))
    (hasOtherColorInDir ?x2 ?y2 ?dir))
(<= (occupied ?x ?y)
    (true (cell ?x ?y ?anyColor)))
(<= (empty ?x ?y)
    (index ?x)
    (index ?y)
    (not (occupied ?x ?y)))
(<= (playerCanMoveAt ?color ?x ?y)
	(empty ?x ?y)
	(opponent ?color ?otherColor)
	(nextCellInDir ?x ?y ?dir ?x2 ?y2)
	(true (cell ?x2 ?y2 ?otherColor))
	(hasOtherColorInDir ?x2 ?y2 ?dir))
(<= (playerCanMove ?player)
    (playerCanMoveAt ?player ?anyX ?anyY))

(<= (legal ?player noop)
    (true (control ?opponent))
    (opponent ?player ?opponent))
(<= (legal ?player noop)
    (role ?player)
    (not (playerCanMove ?player)))
(<= (legal ?player (move ?x ?y))
    (true (control ?player))
    (playerCanMoveAt ?player ?x ?y))


(<= (next (control ?nextPlayer))
    (true (control ?curPlayer))
    (opponent ?curPlayer ?nextPlayer))

(<= (affected ?x2 ?y2 ?dir ?otherColor)
    (does ?player (move ?x1 ?y1))
    (opponent ?player ?otherColor)
    (nextCellInDir ?x1 ?y1 ?dir ?x2 ?y2)
    (hasOtherColorInDir ?x2 ?y2 ?dir)
    (true (cell ?x2 ?y2 ?otherColor)))
(<= (affected ?x2 ?y2 ?dir ?color)
    (hasOtherColorInDir ?x2 ?y2 ?dir)
    (oppDir ?dir ?oppDir)
    (nextCellInDir ?x2 ?y2 ?oppDir ?x1 ?y1)
    (affected ?x1 ?y1 ?dir ?color)
    (true (cell ?x2 ?y2 ?color)))
(<= (affectedCell ?x ?y)
    (affected ?x ?y ?anyDir ?anyColor))

(<= (next (cell ?x ?y ?color))
    (true (cell ?x ?y ?color))
    (not (affectedCell ?x ?y)))
(<= (next (cell ?x ?y ?color))
    (does ?color (move ?x ?y)))
(<= (next (cell ?x ?y ?color))
    (does ?color (move ?anyX ?anyY))
    (affectedCell ?x ?y))



(<= terminal
    (not (playerCanMove black))
    (not (playerCanMove red)))


; goals are nasty
(<= (pieceCount 1 1 ?color 1)
    (true (cell 1 1 ?color)))
(<= (pieceCount 1 1 ?color 0)
    (role ?color)
    (not (true (cell 1 1 ?color))))
(<= (pieceCount ?x ?yp1 ?color ?np1)
    (add ?y p1 ?yp1)
    (pieceCount ?x ?y ?color ?n)
    (succ ?n ?np1)
    (true (cell ?x ?yp1 ?color)))
(<= (pieceCount ?x ?yp1 ?color ?n)
    (add ?y p1 ?yp1)
    (pieceCount ?x ?y ?color ?n)
    (not (true (cell ?x ?yp1 ?color))))
(<= (pieceCount ?xp1 1 ?color ?np1)
    (add ?x p1 ?xp1)
    (pieceCount ?x 8 ?color ?n)
    (succ ?n ?np1)
    (true (cell ?xp1 1 ?color)))
(<= (pieceCount ?xp1 1 ?color ?n)
    (add ?x p1 ?xp1)
    (pieceCount ?x 8 ?color ?n)
    (not (true (cell ?xp1 1 ?color))))
(<= (totalCount ?player ?n)
    (pieceCount 8 8 ?player ?n))
(<= (winner black)
    (totalCount black ?blackCount)
    (totalCount red ?redCount)
    (lessThan ?redCount ?blackCount))
(<= (winner red)
    (totalCount black ?blackCount)
    (totalCount red ?redCount)
    (lessThan ?blackCount ?redCount))
(<= (goal ?player 100)
    (winner ?player))
(<= (goal ?player 0)
    (winner ?opponent)
    (opponent ?player ?opponent))
(<= (goal ?player 50)
	(role ?player)
	(not (winner black))
	(not (winner red)))


(<= (nextCellInDir ?x1 ?y1 ?dir ?x2 ?y2)
    (index ?x1)
    (index ?y1)
    (dir ?dir ?xDelta ?yDelta)
    (add ?x1 ?xDelta ?x2)
    (add ?y1 ?yDelta ?y2))

(<= (base (cell ?x ?y ?color))
    (index ?x)
    (index ?y)
    (role ?color))
(<= (base (control ?player))
    (role ?player))
(<= (input ?player noop)
    (role ?player))
(<= (input ?player (move ?x ?y))
    (role ?player)
    (index ?x)
    (index ?y))

(add 1 p1 2)
(add 2 p1 3)
(add 3 p1 4)
(add 4 p1 5)
(add 5 p1 6)
(add 6 p1 7)
(add 7 p1 8)
(add 1 0 1)
(add 2 0 2)
(add 3 0 3)
(add 4 0 4)
(add 5 0 5)
(add 6 0 6)
(add 7 0 7)
(add 8 0 8)
(add 8 n1 7)
(add 7 n1 6)
(add 6 n1 5)
(add 5 n1 4)
(add 4 n1 3)
(add 3 n1 2)
(add 2 n1 1)

(index 1)
(index 2)
(index 3)
(index 4)
(index 5)
(index 6)
(index 7)
(index 8)

(<= (lessThan ?x ?z)
    (succ ?x ?y)
    (lessThan ?y ?z))
(<= (lessThan ?x ?y)
    (succ ?x ?y))

(succ 0 1)
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)
(succ 19 20)
(succ 20 21)
(succ 21 22)
(succ 22 23)
(succ 23 24)
(succ 24 25)
(succ 25 26)
(succ 26 27)
(succ 27 28)
(succ 28 29)
(succ 29 30)
(succ 30 31)
(succ 31 32)
(succ 32 33)
(succ 33 34)
(succ 34 35)
(succ 35 36)
(succ 36 37)
(succ 37 38)
(succ 38 39)
(succ 39 40)
(succ 40 41)
(succ 41 42)
(succ 42 43)
(succ 43 44)
(succ 44 45)
(succ 45 46)
(succ 46 47)
(succ 47 48)
(succ 48 49)
(succ 49 50)
(succ 50 51)
(succ 51 52)
(succ 52 53)
(succ 53 54)
(succ 54 55)
(succ 55 56)
(succ 56 57)
(succ 57 58)
(succ 58 59)
(succ 59 60)
(succ 60 61)
(succ 61 62)
(succ 62 63)
(succ 63 64)