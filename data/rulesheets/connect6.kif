; Connect 6 19x19, LG Rules
; GDL by Richard Emslie
; (based of Gomoku 11x11 - by Alex Landau)

; Note: there is no game step.  I am assuming that we only end a draw if the board is full and no 6
; in row exists.

(role black)
(role white)

(init (control white_turn0))
(init (cell 10 10 black))

(<= (legal ?player noop)
    (role ?player)
    (not (hasControl ?player)))

(<= (legal ?player (place ?x ?y))
    (hasControl ?player)
    (empty ?x ?y))

(<= (hasControl white)
    (true (control white_turn0)))

(<= (hasControl white)
    (true (control white_turn1)))

(<= (hasControl black)
    (true (control black_turn0)))

(<= (hasControl black)
    (true (control black_turn1)))

(<= (empty ?x ?y)
    (index ?x)
    (index ?y)
    (not (occupied ?x ?y)))

(<= (occupied ?x ?y)
    (true (cell ?x ?y ?color)))

(<= anyEmptyCell
    (empty ?x ?y))

(<= (next (control white_turn1))
    (true (control white_turn0)))

(<= (next (control black_turn0))
    (true (control white_turn1)))

(<= (next (control black_turn1))
    (true (control black_turn0)))

(<= (next (control white_turn0))
    (true (control black_turn1)))

(<= (next (cell ?x ?y ?color))
    (true (cell ?x ?y ?color)))

(<= (next (cell ?x ?y ?color))
    (does ?color (place ?x ?y)))

(<= terminal
    (not anyEmptyCell))

(<= terminal
    (hasLine ?player))

(<= (goal ?player 100)
    (hasLine ?player))

(<= (goal ?player 0)
    (opponent ?player ?opponent)
    (hasLine ?opponent))

(<= (goal ?player 50)
    (role ?player)
    (not (hasLine white))
    (not (hasLine black)))

; Horizontal line of 6 or more
(<= (hasLine ?player)
    (true (cell ?x1 ?y ?player))
    (succ ?x1 ?x2)
    (true (cell ?x2 ?y ?player))
    (succ ?x2 ?x3)
    (true (cell ?x3 ?y ?player))
    (succ ?x3 ?x4)
    (true (cell ?x4 ?y ?player))
    (succ ?x4 ?x5)
    (true (cell ?x5 ?y ?player))
    (succ ?x5 ?x6)
    (true (cell ?x6 ?y ?player)))

; Vertical line of 6 or more
(<= (hasLine ?player)
    (true (cell ?x ?y1 ?player))
    (succ ?y1 ?y2)
    (true (cell ?x ?y2 ?player))
    (succ ?y2 ?y3)
    (true (cell ?x ?y3 ?player))
    (succ ?y3 ?y4)
    (true (cell ?x ?y4 ?player))
    (succ ?y4 ?y5)
    (true (cell ?x ?y5 ?player))
    (succ ?y5 ?y6)
    (true (cell ?x ?y6 ?player)))

; Diagonal line of 6 or more
(<= (hasLine ?player)
    (true (cell ?x1 ?y1 ?player))
    (succ ?x1 ?x2)
    (succ ?y1 ?y2)
    (true (cell ?x2 ?y2 ?player))
    (succ ?x2 ?x3)
    (succ ?y2 ?y3)
    (true (cell ?x3 ?y3 ?player))
    (succ ?x3 ?x4)
    (succ ?y3 ?y4)
    (true (cell ?x4 ?y4 ?player))
    (succ ?x4 ?x5)
    (succ ?y4 ?y5)
    (true (cell ?x5 ?y5 ?player))
    (succ ?x5 ?x6)
    (succ ?y5 ?y6)
    (true (cell ?x6 ?y6 ?player)))

; Other diagonal line of 6 or more
(<= (hasLine ?player)
    (true (cell ?x1 ?y1 ?player))
    (succ ?x1 ?x2)
    (succ ?y2 ?y1)
    (true (cell ?x2 ?y2 ?player))
    (succ ?x2 ?x3)
    (succ ?y3 ?y2)
    (true (cell ?x3 ?y3 ?player))
    (succ ?x3 ?x4)
    (succ ?y4 ?y3)
    (true (cell ?x4 ?y4 ?player))
    (succ ?x4 ?x5)
    (succ ?y5 ?y4)
    (true (cell ?x5 ?y5 ?player))
    (succ ?x5 ?x6)
    (succ ?y6 ?y5)
    (true (cell ?x6 ?y6 ?player)))

(opponent white black)
(opponent black white)
(index 1)
(index 2)
(index 3)
(index 4)
(index 5)
(index 6)
(index 7)
(index 8)
(index 9)
(index 10)
(index 11)
(index 12)
(index 13)
(index 14)
(index 15)
(index 16)
(index 17)
(index 18)
(index 19)

; Note: We extend the successor function past the indices to allow us to
; confirm that the space past the end of the line is not the player's color,
; in a general way.
(succ 1 2)
(succ 2 3)
(succ 3 4)
(succ 4 5)
(succ 5 6)
(succ 6 7)
(succ 7 8)
(succ 8 9)
(succ 9 10)
(succ 10 11)
(succ 11 12)
(succ 12 13)
(succ 13 14)
(succ 14 15)
(succ 15 16)
(succ 16 17)
(succ 17 18)
(succ 18 19)

(base (control white_turn0))
(base (control white_turn1))
(base (control black_turn0))
(base (control black_turn1))

(<= (base (cell ?x ?y ?player))
    (index ?x)
    (index ?y)
    (role ?player))

(<= (input ?player noop)
    (role ?player))

(<= (input ?player (place ?x ?y))
    (role ?player)
    (index ?x)
    (index ?y))
